module ch.fhnw.fregels.messages.textdocument.completion.Completion where

import Compiler.types.Tokens(Token, KeyTk)
import Compiler.types.Global(Global, Symbol, IdInfo)
import Data.HashMap(HashMap)
import Compiler.types.QNames(QName)

import ch.fhnw.fregels.effects.MonadCompile(MonadCompile())
import ch.fhnw.fregels.effects.MonadEnv(MonadEnv())
import ch.fhnw.fregels.effects.MonadGlobals(MonadGlobals(waitForCompilation, readGlobals))
import ch.fhnw.fregels.effects.MonadLog(MonadLog(logInfo))
import ch.fhnw.fregels.messages.textdocument.completion.CompletionTypes(CompletionList(CompletionList), CompletionItem(), CompletionRequestParams())
import ch.fhnw.fregels.messages.textdocument.completion.RecordFieldCompletion(findMembers)
import ch.fhnw.fregels.messages.textdocument.completion.ModuleMemberCompletion(findModuleMembers)
import ch.fhnw.fregels.messages.GeneralLspTypes(Position(), URI())
import ch.fhnw.fregels.utils.Utils(tokenAt, findGlobal)

processCompletionRequest :: (
    MonadCompile m
  , MonadEnv     m
  , MonadFail    m
  , MonadGlobals m
  , MonadLog     m
  ) => CompletionRequestParams -> m CompletionList
processCompletionRequest params =
  case params.context >>= _.triggerCharacter of
    Just "."  -> do
      -- We may not have the latest versions of the compiled files yet, 
      -- therefore we wait for the compiler to finish.
      waitForCompilation maxWaitForCompilationTime
      logInfo "analyzing completions"
      items <- completionList params
      pure CompletionList { isIncomplete = False, items = items }
    otherwise -> pure $ CompletionList { isIncomplete = False, items = [] }
  where
    maxWaitForCompilationTime = 5000

completionList :: (
    MonadCompile m
  , MonadEnv     m
  , MonadFail    m
  , MonadGlobals m
  ) => CompletionRequestParams -> m [CompletionItem]
completionList params = do
  globals     <- readGlobals ()
  let pos = params.position.{character <- (\c -> c - 2)} -- we want the token two positions before the cursor (one position before the cursor is the `.`)
  (token, sourceGlobal)   <- findTokenAtPositionInUri pos params.textDocument.uri globals
  case tokenToQName token sourceGlobal of
    -- lets find all members of the token
    Just qname -> pure $ findMembers qname sourceGlobal
    -- cannot resolve token. Try to load the members of the module 
    Nothing    -> findModuleMembers token sourceGlobal
    
findTokenAtPositionInUri :: MonadFail m => Position -> URI -> HashMap URI Global -> m (Token, Global)
findTokenAtPositionInUri pos uri globals = do
  global <-  findGlobal uri globals
  token  <-  tokenAt pos global
  pure (token, global)

tokenToQName :: Token -> Global -> Maybe QName
tokenToQName tok sourceGlobal = do
  case sourceGlobal.resolved tok of
    Just (Right qname) -> Just qname
    _ -> Nothing

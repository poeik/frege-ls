module ch.fhnw.lsp.messages.Messages where

import ch.fhnw.lsp.messages.initialize.Initialize
import ch.fhnw.lsp.messages.initialize.InitializeTypes
import ch.fhnw.lsp.messages.textdocument.definition.Definition
import ch.fhnw.lsp.messages.textdocument.definition.DefinitionTypes
import ch.fhnw.lsp.messages.textdocument.didopen.DidOpen
import ch.fhnw.lsp.messages.textdocument.didopen.DidOpenTypes
import ch.fhnw.lsp.messages.textdocument.didsave.DidSave
import ch.fhnw.lsp.messages.textdocument.didsave.DidSaveTypes
import ch.fhnw.lsp.messages.textdocument.completion.Completion
import ch.fhnw.lsp.messages.textdocument.completion.CompletionTypes
import ch.fhnw.lsp.messages.GeneralLspTypes

import ch.fhnw.lsp.env.LspEnv

import Data.JSON

processMessage :: Request -> LspEnv IO Response
processMessage (InitializeRequest id params) = fmap (InitializeResponse id) (processInitializeMessage params)
processMessage InitializedRequest            = pure EmptyResponse
processMessage (DefinitionRequest id params) = fmap (DefinitionResponse id) (processDefinitionMessage params)
processMessage (DidOpenNotification params)  = fmap (const EmptyResponse)   (processDidOpenNotification params)
processMessage (DidSaveNotification params)  = fmap (const EmptyResponse)   (processDidSaveNotification params)
processMessage (CompletionRequest id params) = fmap (CompletionResponse id) (processCompletionRequest params)

-- All supported requests
data Request = InitializeRequest      { id :: Int, initParams       :: InitializeRequestParams } 
             | DefinitionRequest      { id :: Int, defParams        :: DefinitionRequestParams }
             | CompletionRequest      { id :: Int, completionParams :: CompletionRequestParams }
             | DidOpenNotification    {            didOpenParams    :: DidOpenRequestParams }
             | DidSaveNotification    {            didSaveParams    :: DidSaveNotificationParams }
             | InitializedRequest 

instance FromJSON Request where
  fromJSON (Struct as) = do
    method <- field "method" as
    case method of 
      "initialize" -> do
        id         <- field "id" as
        initParams <- field "params" as
        pure InitializeRequest { id, initParams }
      "initialized" -> do
        pure InitializedRequest
      "textDocument/definition" -> do
        id        <- field "id" as
        defParams <- field "params" as
        pure DefinitionRequest { id, defParams }
      "textDocument/didOpen" -> do
        didOpenParams <- field "params" as
        pure DidOpenNotification { didOpenParams }
      "textDocument/didSave" -> do
        didSaveParams <- field "params" as
        pure DidSaveNotification { didSaveParams }
      "textDocument/completion" -> do
        id               <- field "id" as
        completionParams <- field "params" as
        pure CompletionRequest { id, completionParams }
      unsupported -> do
        fail $ "Unsupported message with method \"" ++ unsupported ++ "\" received"
  fromJSON garbage = fail ("couldn't decode message from: " ++ show garbage)

-- all supported responses
data Response = InitializeResponse Int InitializeResult
              | DefinitionResponse Int Location 
              | CompletionResponse Int CompletionList
              | EmptyResponse

derive Show Response

instance ToJSON Response where
  toJSON (InitializeResponse requestId result) = 
    Struct [ 
              ("jsonrpc", toJSON "2.0"),
              ("id"     , toJSON requestId),
              ("result" , toJSON result)
           ]
  toJSON EmptyResponse = Struct []
  toJSON (DefinitionResponse requestId location) = 
    Struct [ 
              ("jsonrpc", toJSON "2.0"),
              ("id"     , toJSON requestId),
              ("result" , toJSON location)
           ]
  toJSON (CompletionResponse id completionList) = 
    Struct [ 
              ("jsonrpc", toJSON "2.0"),
              ("id",      toJSON id),
              ("result", toJSON completionList)
           ]

data Method = Method String

instance FromJSON Method where
  fromJSON (Struct as) = do
    method <- field "method" as
    pure $ Method method
  fromJSON garbage = fail ("couldn't decode Method: " ++ show garbage)


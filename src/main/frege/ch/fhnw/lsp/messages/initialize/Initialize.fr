module ch.fhnw.lsp.messages.initialize.Initialize where

import ch.fhnw.lsp.env.LspEnv
import ch.fhnw.lsp.env.LspState
import ch.fhnw.lsp.messages.initialize.InitializeTypes
import ch.fhnw.lsp.compile.CompileExecutorLSP(fromGradle, fromOptionsLSP)
import ch.fhnw.lsp.compile.CompileOptions(standardCompileOptions)
import ch.fhnw.lsp.messages.GeneralLspTypes
import Compiler.types.Global (Options)
import Java.IO as JIO()

processInitializeMessage :: InitializeRequestParams -> LspEnv IO InitializeResult
processInitializeMessage (init@(InitializeRequestParams _ _ _ _)) = do
  logInfo $ logClientInfos init
  let opt = standardCompileOptions
  setupCompileDir opt
  rootDir <- liftResult $ readRootDir init.workspaceFolders
  global  <- liftMonad $ fromOptionsLSP opt

  modify (\cur -> cur.{ projectGlobal = Just global, rootPath = Right rootDir })
  pure $ InitializeResult capabilities serverInfos

logClientInfos ::  InitializeRequestParams -> String
logClientInfos (InitializeRequestParams pid client workspaceFolders rootUri) = let 
    clientInfos = case client of
      Just client -> " to " ++ client.name ++ maybe "" ((++) ", version ") client.version
      Nothing     -> ""
    root    = maybe "" (((++) ", rootUri: ") . show ) rootUri
    ws      = fromMaybe [] workspaceFolders
    folders = fold (\acc cur -> acc ++ ", " ++ show cur) "" ws
  in
    "Connected" ++ clientInfos ++ root ++ ", workspace folders: " ++ folders

readRootDir :: Maybe [WorkspaceFolder] -> ServerResult URI
readRootDir (Just (_:_:_)) = report "The Frege language server does not support multi root projects"
readRootDir (Just (r:[]))  = Right r.uri
readRootDir _              = report "Please specify a root directory"

--- The output directory needs  to be created before the compiler runs the
--- first time. Otherwise we get compiler errors
setupCompileDir :: Options -> LspEnv IO ()
setupCompileDir opt = do
  let outputdir = opt.dir
  let dir = JIO.File.new outputdir
  liftIO dir.mkdirs
  pure ()

serverInfos :: Maybe ServerInfo
serverInfos = Just $ 
  ServerInfo { 
    name = "new Frege LSP"
  , version = Just "v0.0.1"
  }

capabilities :: ServerCapabilities 
capabilities = ServerCapabilities {
  definitionProvider = True 
, textDocumentSync   = TextDocumentSyncOptions {
    openClose = True
  , change = Full 
  , save = SaveOptions { includeText = True }
  }
, completionProvider = CompletionOptions {
    triggerCharacters = ["."]
  , resolveProvider   = False
  }
, hoverProvider = True
}

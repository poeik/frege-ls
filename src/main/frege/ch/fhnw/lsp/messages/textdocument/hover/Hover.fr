module ch.fhnw.lsp.messages.textdocument.hover.Hover where

import Compiler.types.Global(Global)
import Compiler.types.QNames(QName)
import Compiler.types.Tokens(Token)
import Data.HashMap(HashMap)

import ch.fhnw.lsp.effects.MonadLog(MonadLog(logInfo))
import ch.fhnw.lsp.effects.MonadGlobals(MonadGlobals(readGlobals))
import ch.fhnw.lsp.messages.GeneralLspTypes(URI(), MarkupContent(), Markdown, Position(Position), Range(Range))
import ch.fhnw.lsp.messages.textdocument.hover.HoverTypes(HoverRequestParams(), HoverResult(HoverResult))
import ch.fhnw.lsp.utils.Utils(tokenAt, findGlobal)
import ch.fhnw.lsp.utils.Documentation(documentSymbol)

processHoverRequest :: 
  (
    MonadFail m
  , MonadGlobals m
  , MonadLog m
  ) => HoverRequestParams -> m HoverResult
processHoverRequest params = do
  let currentUri = params.textDocument.uri
  let cursorPos  = params.position
  logInfo $ "requested hover for: " ++ currentUri.path ++  " at position: " ++ show cursorPos
  (documentation, range) <- hoverInformation currentUri cursorPos
  pure $ HoverResult { contents = documentation, range = range }

hoverInformation :: 
  (
    MonadFail m
  , MonadGlobals m
  ) => URI -> Position -> m (MarkupContent, Range)
hoverInformation uri position = do
  globals                 <- readGlobals ()
  (token, sourceGlobal)   <- findToken position uri globals
  case tokenToQName token sourceGlobal >>= sourceGlobal.findit of
    Just sym -> pure $ (documentSymbol sourceGlobal sym, tokenToRange token)
    Nothing    -> fail "Could not find hover information for requested position"
    
findToken :: MonadFail m => Position -> URI -> HashMap URI Global -> m (Token, Global)
findToken pos uri globals = do
  global <- findGlobal uri globals
  token  <- tokenAt pos
    global
  pure (token, global)

tokenToQName :: Token -> Global -> Maybe QName
tokenToQName tok sourceGlobal = do
  case sourceGlobal.resolved tok of
    Just (Right qname) -> Just qname
    _ -> Nothing

tokenToRange :: Token -> Range
tokenToRange tok = Range { start, end }
  where 
    start = Position { line = tok.line, character = tok.col }
    end   = Position { line = tok.line, character = tok.col + tok.length }
